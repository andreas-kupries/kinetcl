#!/usr/bin/env tclsh8.5
## -*- tcl -*-
# # ## ### ##### ######## ############# #####################
## Requirements

package require kinetcl
package require crimp::ppm
package require crimp::tk

# # ## ### ##### ######## ############# #####################

proc main {} {
    cmdline
    setup
    run
    return
}

# # ## ### ##### ######## ############# #####################

proc cmdline {} {
    global argv tracedir
    if {[llength $argv] != 1} usage
    lassign $argv tracedir
    return
}

proc usage {} {
    global argv0
    puts stderr "usage: $argv0 tracedir"
    exit 1
}

proc setup {} {
    global tracedir photo sat
    file mkdir $tracedir

    kinetcl depth create K

    set size [dict get [K meta] map res]

    set photo [image create photo]
    crimp write 2tk $photo [crimp blank grey8 {*}$size 0]

    set sat [crimp blank grey8 {*}$size 128]

    ttk::label  .image -image $photo
    ttk::button .exit -text Exit -command ::exit

    # More buttons: stop/start recording

    pack .image -side top
    pack .exit  -side left

    K bind newdata newframe_depth
    bind all <q> ::exit

    update idletasks
    return
}

proc run {} {
    ping start...
    kinetcl start
    ping process...
    while {1} record
    return
}

proc ping {text} {
    puts -nonewline \r$text
    flush stdout
    return
}

# # ## ### ##### ######## ############# #####################

proc newframe_depth {args} {
    global tracedir photo sat
    set s [clock milliseconds]

    set image [K map]
    set frame [K frame]

    # New way of visualizing the grey16 depth image: Split its pixels
    # into msb and lsb. Then make MSB the hue, and LSB the inverted
    # value of a HSV, use a fixed saturation. Convert this to RGB.
    #
    # Now large depth changes are color coded, with the details in the
    # luminance.

    #set image [crimp scale $image 2]
    lassign [crimp split $image] msb lsb
    set msb [crimp scale $msb 16]
    set lsb [crimp invert $lsb]
    set image [crimp convert 2rgb [crimp join 2hsv $msb $sat $lsb]]

    crimp write 2file ppm-raw $tracedir/depth-[format %04d $frame].ppm $image

    crimp write 2tk $photo $image
    .image configure -image $photo

    set e [clock milliseconds]
    ping "[expr {$e - $s}] $frame        "
    return
}

proc record {} {
    kinetcl waitUpdate
    kinetcl estop
    update
    kinetcl estart
    return
}

# # ## ### ##### ######## ############# #####################
main
exit
