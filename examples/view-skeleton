#!/usr/bin/env tclsh8.5
## -*- tcl -*-
# # ## ### ##### ######## ############# #####################
## Requirements

package require kinetcl
package require Tk
package require crimp::tk
package require crimp

# # ## ### ##### ######## ############# #####################

# Data structures explanation.
#
# S = global dictionary holding all user skeleton data.
#
#   S <user> joints <joint>            --> joint confidence and real-world position.
#   S <user> canvas <joint>            --> canvas item representing the joint (dot)
#   S <user> canvas <jointA>,<jointB>  --> canvas item representing the line between
#                                          two joints (segment).
#   S <user> at     <joint>            --> joint position in projective coordinates
#
# Canvas items:
#             What    Attributes                  Tags
#             ====    ==========                  ====
#   Joints:   Circle  red, radius, see [setup]    u,<user> p,<user>,<joint>
#   Segments: Line    green, width=radius         u,<user> l,<user>,<jointA>,<jointB>
#             ====    ==========                  ====

# Eventflow
#
# K (user-new) --> (*) [request-calibration]    <------\
#                  --> K (calibration-complete) /!ok --/
#                  --> K (calibration-complete) /ok 
#                      --> [start-tracking] (*)
#
# K (user-exit) --> Kill data structures, kill drawing elements.
#                   Tracking of the user, if active, continues
#                   inside the system.
#
# K (user-lost) --> Kill data structures, kill drawing elements.
#                   Tracking is implicitly stopped for the user.
#
# K (newframe) --> [newframe_user]
#                  per-user: ignore !tracking (See *)
#                  get/update skeletons and drawings.
#
# D (newframe) --> get/update shown depth image.

# # ## ### ##### ######## ############# #####################

proc main {} {
    cmdline
    setup
    run
    return
}

# # ## ### ##### ######## ############# #####################

proc cmdline {} {
    global argv
    if {[llength $argv]} usage
    return
}

proc usage {} {
    global argv0
    puts stderr "usage: $argv0"
    exit 1
}

proc setup {} {
    global photo radius dotc segc textc
    set radius 5
    set dotc red
    set segc lightblue
    set textc green

    kinetcl user create K
    K set-callback-user-new             usernew
    K set-callback-user-exit            userexit
    K set-callback-user-lost            userlost
    K set-callback-calibration-complete calibration
    K set-callback-newdata              newframe_user
    K set-profile all

    kinetcl depth create D
    D set-callback-newdata newframe_depth

    set size [dict get [D meta] map res]
    lassign $size w h

    puts [join $size { x }]

    set photo [image create photo]
    crimp write 2tk $photo [crimp blank grey8 $w $h 0]

    canvas      .image \
	-scrollregion [list 0 0 $w $h] \
	-width $w -height $h
    ttk::button .exit -text Exit -command ::exit

    .image create image 0 0 -tags PHOTO -anchor nw
    render

    # More buttons: stop/start recording
    pack .image -side top -expand 1 -fill both
    pack .exit  -side left

    bind all <q> ::exit

    update idletasks
    return
}

proc render {} {
    global photo
    .image itemconfigure PHOTO -image $photo
    .image coords        PHOTO 0 0
    return
}

proc run {} {
    ping start...
    kinetcl start
    ping process...
    while {1} record
    return
}

proc ping {text} {
    puts -nonewline \r$text
    flush stdout
    return
}

# # ## ### ##### ######## ############# #####################

proc newframe_depth {args} {
    global photo
    set s [clock milliseconds]

    set image [D map]
    set frame [D frame]

    # Plain contrast compressed gray scale visualization.
    set image [crimp::FITFLOAT [crimp convert 2float $image]] 

    set size [crimp dimensions $image]

    crimp write 2tk $photo $image
    render

    set e [clock milliseconds]
    #ping "[expr {$e - $s}] $frame ($size)       "
    return
}

# # ## ### ##### ######## ############# #####################

proc usernew {event obj user} {
    puts "$event $user"
    K request-calibration $user 0
    return
}

proc userexit {event obj user} {
    global S
    puts "$event $user"
    catch { dict unset S $user }
    .image delete u,$user
    puts "delete u,$user = ([.image find withtag u,$user])"
    return
}

proc userlost {event obj user} {
    global S
    puts "$event $user"
    catch { dict unset S $user }
    .image delete u,$user
    puts "delete u,$user = ([.image find withtag u,$user])"
    return
}

# # ## ### ##### ######## ############# #####################

proc calibration {event obj user status} {
    puts "$event $user : $status"
    if {$status eq "ok"} {
	K start-tracking $user
    } else {
	K request-calibration $user 0
    }
    return
}

# # ## ### ##### ######## ############# #####################

proc newframe_user {args} {
    puts "$args"
    foreach user [K users] {
	labeluser $user
	skeletonfor $user
    }
    return
}

proc skeletonfor {user} {
    global S
    if {![K is-tracking $user]} return
    puts "\tupdate $user"
    #puts @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
    #memory validate on
    dict set S $user joints [K get-skeleton $user]
    draw $user
    #memory validate off
    return
}

proc labeluser {user} {
    global S
    set at [lrange [lindex [D world2projective [K centerof $user]] 0] 0 1]
    if {![info exists S] || ![dict exists $S $user canvas label]} {
	set label [make-text .image {*}$at \
		       -font {Helvetica 20} \
		       -tags [list t,$user u,$user]]
	dict set S $user canvas label $label
    }
    .image itemconfigure t,$user -text [getlabel $user]
    place-text .image t,$user {*}$at
    return
}

proc getlabel {user} {
    set text "User $user"
    if {[K is-tracking $user]} {
	append text " tracking"
    } elseif {[K is-calibrated $user]} {
	append text " calibrated"
    } elseif {[K is-calibrating $user]} {
	append text " calibrating"
    }
    return $text
}

proc draw {user} {
    drawpoints $user
    drawlines  $user
}

proc drawpoints {user} {
    global S
    array set joint [dict get $S $user joints]
    catch {
	array set canvas [dict get $S $user canvas]
    }

    foreach j [array names joint] {
	lassign [lindex $joint($j) 0] confidence point

	if {$confidence < 0.5} {
	    if {[llength [.image find withtag p,$user,$j]]} {
		puts "\tdrop non-confident $user $j"
		.image delete p,$user,$j
		catch { dict unset S $user canvas $j }
		catch { dict unset S $user at $j }
	    }
	} else {
	    if {![info exists canvas($j)]} {
		puts "\tadd confident $user $j"
		set dot [dot .image 0 0 -tags [list p,$user,$j u,$user]]
		dict set S $user canvas $j $dot
	    }
	    #puts ==================================================
	    #puts "($user $j): W $point"
	    set p [lindex [D world2projective $point] 0]
	    dict set S $user at $j $p
	    #puts "($user $j): S $p"
	    lassign $p x y z
	    place-dot .image p,$user,$j $x $y
	}
    }
    return
}

proc drawlines {user} {
    global S
    array set canvas {}
    catch {
	array set canvas [dict get $S $user canvas]
    }
    array set at {}
    catch {
	array set at [dict get $S $user at]
    }

    # not used: fingertip, wrist, ankle, collars, waist
    foreach {s e} {
	left-hand       left-elbow
	left-elbow      left-shoulder
	left-shoulder   torso
	left-shoulder   neck

	left-foot       left-knee
	left-knee       left-hip
	left-hip        torso

	right-hand      right-elbow
	right-elbow     right-shoulder
	right-shoulder  torso
	right-shoulder  neck

	right-foot      right-knee
	right-knee      right-hip
	right-hip       torso

	head            neck
	left-hip        right-hip
    } {
	if {![info exists canvas($s)] ||
	    ![info exists canvas($e)]} {
	    .image delete l,$user,$s,$e
	    catch { dict unset S $user canvas $s,$e }
	} else {
	    lassign $at($s) x0 y0
	    lassign $at($e) x1 y1
	    if {![info exists canvas($s,$e)]} {
		set segment [segment .image $x0 $y0 $x1 $y1 -tags [list l,$user,$s,$e u,$user]]
		dict set S $user canvas $s,$e $segment
	    }
	    place-segment .image l,$user,$s,$e $x0 $y0 $x1 $y1
	}
    }
    return
}

proc place-dot {c item x y} {
    global radius
    set w [expr {$x - $radius}]
    set n [expr {$y - $radius}]
    set e [expr {$x + $radius}]
    set s [expr {$y + $radius}]
    $c coords $item $w $n $e $s
    return
}

proc dot {c x y args} {
    global radius dotc
    # Create a circle centered on the chosen location, red filled
    # with black border.
    set w [expr {$x - $radius}]
    set n [expr {$y - $radius}]
    set e [expr {$x + $radius}]
    set s [expr {$y + $radius}]
    return [$c create oval $w $n $e $s \
		-width   1   \
		-outline $dotc \
		-fill    $dotc \
		{*}$args]
}

proc place-segment {c item x0 y0 x1 y1} {
    $c coords $item $x0 $y0 $x1 $y1
    return
}

proc segment {c x0 y0 x1 y1 args} {
    global radius segc
    return [$c create line $x0 $y0 $x1 $y1 \
		-width $radius \
		-fill $segc \
		{*}$args]
}

proc place-text {c item x y} {
    $c coords $item $x $y
    return
}

proc make-text {c x y args} {
    global textc
    return [$c create text $x $y \
		-fill $textc -text {} \
		{*}$args]
}

proc record {} {
    kinetcl waitUpdate
    kinetcl estop
    update
    kinetcl estart
    return
}

# # ## ### ##### ######## ############# #####################
main
exit
