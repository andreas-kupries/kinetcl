#!/usr/bin/env tclsh8.5
## -*- tcl -*-
# # ## ### ##### ######## ############# #####################
## Requirements

package require kinetcl

# # ## ### ##### ######## ############# #####################

proc main {} {
    cmdline
    setup
    run
    return
}

# # ## ### ##### ######## ############# #####################

proc cmdline {} {
    global argv
    if {[llength $argv]} usage
    return
}

proc usage {} {
    global argv0
    puts stderr "usage: $argv0"
    exit 1
}

proc setup {} {
    kinetcl user create K
    K set-profile all

    K bind user-new             usernew	   
    K bind user-lost            userlost   
    K bind calibration-complete calibration
    return
}

proc run {} {
    ping start...
    kinetcl start
    ping process...
    while {1} record
    return
}

proc ping {text} {
    puts -nonewline \r$text
    flush stdout
    return
}

# # ## ### ##### ######## ############# #####################

proc usernew {event obj details} {
    dict with details {} ; # user
    puts "$event $user"
    K request-calibration $user 0
    return
}

proc userlost {event obj details} {
    dict with details {} ; # user
    puts "$event $user"
    return
}

proc calibration {event obj details} {
    dict with details {} ; # user status
    puts "$event $user : $status"
    if {$status eq "ok"} {
	K start-tracking $user
    } else {
	K request-calibration $user 0
    }
    return
}

proc skeleton {} {
    foreach user [K users] {
	skeletonfor $user
    }
    return
}

proc skeletonfor {user} {
    if {![K is-tracking $user]} return
    array set $user [K get-skeleton $user]
    puts "skeleton $user ____________________________________"
    parray $user
    return
}

proc record {} {
    kinetcl waitUpdate
    kinetcl estop
    update
    kinetcl estart
    skeleton
    return
}

# # ## ### ##### ######## ############# #####################
main
exit
