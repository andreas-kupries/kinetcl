#!/usr/bin/env tclsh8.5
## -*- tcl -*-
# # ## ### ##### ######## ############# #####################
## Requirements

package require kinetcl

# # ## ### ##### ######## ############# #####################

proc main {} {
    cmdline
    setup
    run
    return
}

# # ## ### ##### ######## ############# #####################

proc cmdline {} {
    global argv
    if {[llength $argv]} usage
    return
}

proc usage {} {
    global argv0
    puts stderr "usage: $argv0"
    exit 1
}

proc setup {} {
    kinetcl user create K

    K bind user-enter userchange
    K bind user-exit  userchange
    K bind user-new   userchange
    K bind user-lost  userchange

    K bind calibration-start    calibration
    K bind calibration-complete calibration
    #K bind calibration-progress calibration
    return
}

proc run {} {
    ping start...
    kinetcl start
    ping process...
    while {1} record
    return
}

proc ping {text} {
    puts -nonewline \r$text
    flush stdout
    return
}

# # ## ### ##### ######## ############# #####################

proc calibration {event obj details} {
    set status n/a
    dict with details {} ; # user (status)
    puts "\t$event $user : $status"
    if {$event ne "calibration-complete"} return
    if {$status ne "ok"} {
	K request-calibration $user 0
	return
    }
    K start-tracking $user
    return
}

proc userchange {event obj details} {
    dict with details {} ; # user
    puts "$event $user"

    switch -- $event {
	user-new -
	user-enter {
	    K request-calibration $user 0
	}
	user-exit {
	    # Note: The user may exit while calibration is still
	    # running, i.e. when tracking hasn't started yet. Easier
	    # to catch the error here, instead of tracking exactly
	    # when tracking has started for a specific user.
	    catch {
		K stop-tracking $user
	    }
	}
    }
    return
}

proc record {} {
    kinetcl waitUpdate
    kinetcl estop
    update
    kinetcl estart
    return
}

# # ## ### ##### ######## ############# #####################
main
exit
