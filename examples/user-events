#!/usr/bin/env tclsh8.5
## -*- tcl -*-
# # ## ### ##### ######## ############# #####################
## Requirements

package require kinetcl

# # ## ### ##### ######## ############# #####################

proc main {} {
    cmdline
    setup
    run
    return
}

# # ## ### ##### ######## ############# #####################

proc cmdline {} {
    global argv
    if {[llength $argv]} usage
    return
}

proc usage {} {
    global argv0
    puts stderr "usage: $argv0"
    exit 1
}

proc setup {} {
    kinetcl user create K

    uevent bind ::K user-enter userchange
    uevent bind ::K user-exit  userchange
    uevent bind ::K user-new   userchange
    uevent bind ::K user-lost  userchange

    uevent bind ::K calibration-start    calibration
    uevent bind ::K calibration-complete calibration
    #uevent bind ::K calibration-progress calibration
    return
}

proc run {} {
    ping start...
    kinetcl start
    ping process...
    while {1} record
    return
}

proc ping {text} {
    puts -nonewline \r$text
    flush stdout
    return
}

# # ## ### ##### ######## ############# #####################

proc calibration {event obj user {status {}}} {
    puts "\t$event $user : $status"
    if {$event ne "calibration-complete"} return
    if {$status ne "ok"} {
	K request-calibration $user 0
	return
    }
    K start-tracking $user
    return
}

proc userchange {event obj user} {
    puts "$event $user"

    switch -- $event {
	user-new -
	user-enter {
	    K request-calibration $user 0
	}
	user-exit {
	    K stop-tracking $user
	}
    }
    return
}

proc record {} {
    kinetcl waitUpdate
    kinetcl estop
    update
    kinetcl estart
    return
}

# # ## ### ##### ######## ############# #####################
main
exit
