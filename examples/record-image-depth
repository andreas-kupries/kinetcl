#!/usr/bin/env tclsh8.5
## -*- tcl -*-
# # ## ### ##### ######## ############# #####################
## Requirements

package require kinetcl
package require crimp::ppm

# # ## ### ##### ######## ############# #####################

proc main {} {
    cmdline
    setup
    run
    return
}

# # ## ### ##### ######## ############# #####################

proc cmdline {} {
    global argv tracedir
    if {[llength $argv] != 1} usage
    lassign $argv tracedir
    return
}

proc usage {} {
    global argv0
    puts stderr "usage: $argv0 tracedir"
    exit 1
}

proc setup {} {
    global tracedir sat
    file mkdir $tracedir
    kinetcl image create I
    kinetcl depth create D

    set size [dict get [D meta] map res]
    set sat  [crimp blank grey8 {*}$size 128]
    return
}

proc run {} {
    ping start...
    kinetcl start
    ping process...
    while {1} record
    return
}

proc ping {text} {
    puts -nonewline \r$text
    flush stdout
    return
}

# # ## ### ##### ######## ############# #####################

proc record {} {
    global tracedir sat
    kinetcl waitUpdate

    set s [clock milliseconds]

    set image  [I map]
    set iframe [I frame]

    set depth  [D map]
    set dframe [D frame]

    # New way of visualizing the grey16 depth image: Split its pixels
    # into msb and lsb. Then make MSB the hue, and LSB the inverted
    # value of a HSV, use a fixed saturation. Convert this to RGB.
    #
    # Now large depth changes are color coded, with the details in the
    # luminance.

    lassign [crimp split $depth] msb lsb
    set depth [crimp convert 2rgb \
		   [crimp join 2hsv \
			$msb \
			$sat \
			[crimp invert $lsb]]]

    crimp write 2file ppm-raw $tracedir/image-[format %04d $iframe].ppm $image
    crimp write 2file ppm-raw $tracedir/depth-[format %04d $dframe].ppm $depth

    set e [clock milliseconds]
    ping "D [expr {$e - $s}] $iframe $dframe        "
    return
}

# # ## ### ##### ######## ############# #####################
main
exit
