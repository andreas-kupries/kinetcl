#!/usr/bin/env tclsh8.5
## -*- tcl -*-
# # ## ### ##### ######## ############# #####################
## Requirements

package require kinetcl
package require crimp::pgm
package require crimp::tk

# # ## ### ##### ######## ############# #####################

proc main {} {
    cmdline
    setup
    run
    return
}

# # ## ### ##### ######## ############# #####################

proc cmdline {} {
    global argv tracedir
    if {[llength $argv] != 1} usage
    lassign $argv tracedir
    return
}

proc usage {} {
    global argv0
    puts stderr "usage: $argv0 tracedir"
    exit 1
}

proc setup {} {
    global tracedir photo
    file mkdir $tracedir

    kinetcl scene create K

    set size [dict get [K meta] map res]

    set photo [image create photo]
    crimp write 2tk $photo [crimp blank grey8 {*}$size 0]

    ttk::label  .image -image $photo
    ttk::button .exit -text Exit -command ::exit

    # More buttons: stop/start recording

    pack .image -side top
    pack .exit  -side left

    uevent bind ::K newdata newframe_scene
    bind all <q> ::exit

    update idletasks
    return
}

proc run {} {
    ping start...
    kinetcl start
    ping process...
    while {1} record
    return
}

proc ping {text} {
    puts -nonewline \r$text
    flush stdout
    return
}

# # ## ### ##### ######## ############# #####################

proc newframe_scene {args} {
    global tracedir photo
    set s [clock milliseconds]

    set image [K map]
    set frame [K frame]

    # Currently the crimp pgm doesn't accept grey16, although the
    # format might be able to.  Because of that we compress to grey8,
    # and try to keep as much of the range as possible. Of course, we
    # also need the compressed version for display with Tk.

    set image [crimp::FITFLOAT [crimp convert 2float $image]]

    crimp write 2file pgm-raw $tracedir/scene-[format %04d $frame].pgm $image
    crimp write 2tk $photo $image
    .image configure -image $photo

    set e [clock milliseconds]
    ping "[expr {$e - $s}] $frame        "
    return
}

proc record {} {
    kinetcl waitUpdate
    kinetcl estop
    update
    kinetcl estart
    return
}

# # ## ### ##### ######## ############# #####################
main
exit
